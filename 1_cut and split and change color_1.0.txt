// This is vesion 1.0
// with number
// Create one more folder.

dir = getDirectory("Choose the folder of target image");

list = getFileList(dir);
check = 0;  // Make sure there is only folders.

for (i=0; i<list.length; i++) {
	// Remove"/", so that the filename will be correct.
	slashIndex = indexOf(list[i], "/");
	if (slashIndex != -1) {
		list[i] = substring(list[i], 0, slashIndex); 
		print(list[i]);
	}
	
	// Make sure there is only folders.
	else {
		print(list[i]);
		print("Only folders can exist!")
		check = 1;
	}
}

// Create folders and put the files into the raw folder
if (check == 0) {
	for (i=0; i<list.length; i++) {
		dir2 = dir + list[i];    // Date + No.
		rawlist = getFileList(dir2);

		// Create folders
		File.makeDirectory(dir2 + File.separator + "raw");
		File.makeDirectory(dir2 + File.separator + "cut");
		File.makeDirectory(dir2 + File.separator + "red");
		File.makeDirectory(dir2 + File.separator + "green");
		File.makeDirectory(dir2 + File.separator + "nucleus mask");
		File.makeDirectory(dir2 + File.separator + "label nucleus mask");
		File.makeDirectory(dir2 + File.separator + "red dot green background");
		File.makeDirectory(dir2 + File.separator + "label with number");
		
		// Put the image into raw file
		for (j=0; j<rawlist.length; j++) {
			File.rename(dir2 + File.separator + rawlist[j], dir2 + File.separator + "raw" + File.separator + rawlist[j]);
		}
	}
}

// Start cutting and splitting
if (check == 0) {
	for (i=0; i<list.length; i++) {
		savepath = dir + list[i] + File.separator + "cut" + File.separator + list[i];
		savepath2 = dir + list[i] + File.separator + "red" + File.separator + list[i];
		savepath3 = dir + list[i] + File.separator + "green" + File.separator + list[i];
		savepath4 = dir + list[i] + File.separator + "nucleus mask" + File.separator + list[i];
		
		dir_raw = dir + list[i] + File.separator + "raw";
		list_raw = getFileList(dir_raw);
		
		for (j=0; j<list_raw.length; j++) {
			file = dir + list[i] + File.separator + "raw" + File.separator + list_raw[j];     // Define the path
			open(file);     // Open the target image
			imagename = getInfo("image.filename");

			// Cut
			run("Canvas Size...", "width=256 height=256 position=Center-Left");
			saveAs("Tiff", savepath + " " + j);

			// Split	
			run("Split Channels");
			selectWindow(list[i] + " " + j + ".tif" + " (blue)");
			close();
			selectWindow(list[i] + " " + j + ".tif" + " (red)");
			saveAs("Tiff", savepath2 + " " + j + ".tif");
			saveAs("Tiff", savepath4 + " " + j + ".tif");
			close();
			selectWindow(list[i] + " " + j + ".tif" + " (green)");
			saveAs("Tiff", savepath3 + " " + j + ".tif");
			close();
			print(i + "." + imagename);
		}
	}
}

// Change color
for (i=0; i<list.length; i++) {
	dir2 = dir + list[i];
	dir_cut = dir2 + File.separator + "cut";
	cut_list = getFileList(dir_cut);

	for (j=0; j<cut_list.length; j++) {
		file = dir_cut + File.separator + cut_list[j];     // Define the path
		print(file);
		filename = dir2 + File.separator + "red dot green background" + File.separator + cut_list[j];		
		open(file);     // Open the target image
		run("Split Channels");
		selectWindow(cut_list[j] + " (blue)");  
		close();
		selectWindow(cut_list[j] + " (green)");
		run("MultiThresholder", "Otsu apply");     // Otsu
		run("Invert");
		run("Analyze Particles...", "clear include add");     // Get G4 foci
		selectWindow(cut_list[j] + " (red)");

		// Set LUT
		reds = newArray(256); 
		greens = newArray(256); 
		blues = newArray(256);
		for (k=0; k<256; k++) {
			reds[k] = 0;
			greens[k] = k*139/256;
			blues[k] = k*40/256;
		}
		setLut(reds, greens, blues);

		run("RGB Color");

		n = roiManager("count");     // n = number of dots
		if (n > 1) {
			selectWindow(cut_list[j] + " (red)");
			roiManager("Combine");
			setForegroundColor(255, 0, 0);
			run("Draw", "slice"); 
		}

		if (n == 1) {
			selectWindow(cut_list[j] + " (red)");
			roiManager("Select", 0);
			setForegroundColor(255, 0, 0);
			run("Draw", "slice");
		}
		saveAs("Tiff", filename);
		close();
		selectWindow(cut_list[j] + " (green)");
		close();
	}
}

// Finish
print("Finished!");